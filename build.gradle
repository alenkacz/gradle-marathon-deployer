buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath "com.avast.gradle:docker-compose-gradle-plugin:0.3.11"
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.5"
    }
}

plugins {
    id 'net.researchgate.release' version '2.4.0'
}

group 'cz.alenkacz.gradle'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'docker-compose'
apply plugin: "com.gradle.plugin-publish"

repositories {
    jcenter()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dockerCompose.isRequiredBy(test)
dockerCompose {
    useComposeFiles = ['docker-compose-test.yml']
    waitForTcpPortsTimeout = java.time.Duration.ofMinutes(1)
}

test.doFirst {
    dockerCompose.exposeAsEnvironment(test)
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.3"
}

dependencies {
    compile gradleApi()
    compile "org.asynchttpclient:async-http-client:2.0.11"
    compile 'org.glassfish.jersey.media:jersey-media-sse:2.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile gradleTestKit()
    testCompile ('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group:'org.codehaus.groovy'
    }
    testCompile 'com.github.mohitsoni:marathon-client:0.4.2'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'gradle-marathon-deployer'
        desc = 'Gradle plugin that will deploy your application to the Marathon cloud.'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alenkacz/gradle-marathon-deployer.git'
        websiteUrl = 'https://github.com/alenkacz/gradle-marathon-deployer'
        issueTrackerUrl = 'https://github.com/alenkacz/gradle-marathon-deployer/issues'
        labels = ['marathon', 'mesos', 'docker', 'deploy', 'cloud']
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

project.ext.set('gradle.publish.key', System.getenv('GRADLE_PORTAL_KEY'))
project.ext.set('gradle.publish.secret', System.getenv('GRADLE_PORTAL_SECRET'))
pluginBundle {
    website = 'https://github.com/alenkacz/gradle-marathon-deployer'
    vcsUrl = 'https://github.com/alenkacz/gradle-marathon-deployer'
    description = 'Gradle plugin that can deploy your application to Marathon'
    tags = ['marathon', 'deploy']

    plugins {
        marathonPlugin {
            id = 'cz.alenkacz.gradle.marathon.deploy'
            displayName = 'Gradle Marathon Deploy plugin'
        }
    }
}